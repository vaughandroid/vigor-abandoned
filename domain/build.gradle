buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "net.ltgt.gradle:gradle-apt-plugin:0.6"
    }
}

apply plugin: 'java'
apply plugin: "net.ltgt.apt"
apply from: "${rootDir}/buildsystem/retrolambda.gradle"

def globalConfig = rootProject.ext.javaConfig
//noinspection GroovyUnusedAssignment
sourceCompatibility = globalConfig.sourceCompatibility
//noinspection GroovyUnusedAssignment
targetCompatibility = globalConfig.targetCompatibility

// Mimic Maven's 'provided' scope for compile-time dependencies.
// See: https://sinking.in/blog/provided-scope-in-gradle/
// TODO: look into compileOnly
configurations {
    provided
}

sourceSets {
    main {
        compileClasspath += configurations.provided
    }
    test {
        compileClasspath += configurations.provided
    }
}

dependencies {
    def javaLibs = rootProject.ext.javaLibs
    def javaTestLibs = rootProject.ext.javaTestLibs

    apt javaLibs.autoValue
    provided javaLibs.autoValue
    apt javaLibs.autoValueWith
    compile javaLibs.dagger
    apt javaLibs.daggerCompiler
    compile javaLibs.guava
    provided javaLibs.javaxAnnotation
    compile javaLibs.jetBrainsAnnotations
    compile javaLibs.rxJava
    compile javaLibs.slf4j

    testCompile javaTestLibs.jUnit
    testCompile javaTestLibs.assertJ
    testCompile javaTestLibs.mockito
}
