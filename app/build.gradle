apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

android {
    def globalConfig = rootProject.ext

    compileSdkVersion globalConfig.androidCompileSdkVersion
    buildToolsVersion globalConfig.androidBuildToolsVersion

    compileOptions {
        sourceCompatibility globalConfig.javaSourceCompatibility
        targetCompatibility globalConfig.javaTargetCompatibility
    }

    defaultConfig {
        applicationId "vaughandroid.vigor"
        minSdkVersion globalConfig.androidMinSdkVersion
        targetSdkVersion globalConfig.androidTargetSdkVersion
        versionCode 1
        versionName "1.0"

        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        debug {
            minifyEnabled false
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        for (fileName in rootProject.ext.packagingExclusions) {
            exclude fileName
        }
    }
}

dependencies {
    def javaLibs        = rootProject.ext.javaLibs
    def androidLibs     = rootProject.ext.androidLibs
    def javaTestLibs    = rootProject.ext.javaTestLibs
    def androidTestLibs = rootProject.ext.androidTestLibs

    compile project(':domain')
    // TODO: This is required so that we can construct the Repositories. Might be able to get around it?
    compile project(':data')

    compile     javaLibs.dagger
    apt         javaLibs.daggerCompiler
    provided    javaLibs.javaxAnnotation
    compile     javaLibs.rxJava

    compile         androidLibs.androidAnnotations
    compile         androidLibs.appCompat
    compile         androidLibs.butterKnife
    apt             androidLibs.butterKnifeCompiler
    compile         androidLibs.design
    debugCompile    androidLibs.leakCanary
    releaseCompile  androidLibs.leakCanaryNoOp
    compile         androidLibs.rxAndroid
    compile         androidLibs.rxBinding
    compile         androidLibs.rxBindingAppCompatV7
    compile         androidLibs.rxBindingDesign
    compile         androidLibs.rxBindingSupportV4
    compile         androidLibs.rxBindingRecyclerViewV7
    compile         androidLibs.slf4jAndroid

    testCompile javaTestLibs.assertJ
    testCompile javaTestLibs.jUnit
    testCompile javaTestLibs.mockito

    androidTestCompile javaTestLibs.assertJ
    androidTestCompile androidTestLibs.dexmaker
    androidTestCompile androidTestLibs.dexmakerMockito
    androidTestCompile androidTestLibs.dexmakerDx
    androidTestCompile androidTestLibs.espresso
    androidTestCompile javaTestLibs.jUnit
    androidTestCompile javaTestLibs.mockito
    androidTestCompile androidTestLibs.testSupportRules
    androidTestCompile androidTestLibs.testSupportRunner

    // Fix dependency version conflicts.
    androidTestCompile androidLibs.androidAnnotations
}
